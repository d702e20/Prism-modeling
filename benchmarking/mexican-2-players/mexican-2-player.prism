// 2-player Mexican Standoff

csg

// Player blocks declares which modules and actions a player has control over
player p1 
	shooter1
endplayer

player p2 
	shooter2
endplayer

const max_health = 1;

// Global variables to track health of the shooters
global health1 : int init max_health;
global health2 : int init max_health;
global p1_lost_hp : bool init false;
global p2_lost_hp : bool init false;

label "p1_lost_hp_round" = p1_lost_hp;
label "p2_lost_hp_round" = p2_lost_hp; 
// Labels to see if the shooters are still alive
label "both_died" = health1 = 0 & health2 = 0;
label "shooter1_won" = health1 > 0 & health2 = 0;
label "shooter2_won" = health2 > 0 & health1 = 0;
label "alive1" = health1 > 0;
label "alive2" = health2 > 0;

// Shooter1 can either wait, and nothing happens. Or if the guard that health1 and health2 is larger than 0 is true, shooter 1 can shoot shooter2
module shooter1
    [p1wait] true -> true;
    [p1shoot2] health1 > 0 & health2 > 0 -> true;
endmodule

// shooter2 uses shooter1 module as template, but with some actions/variables changed. e.g health1 is now health2 in shooter2 
module shooter2 = shooter1 [health1=health2, health2=health1, p1wait=p2wait, p1shoot2=p2shoot1] endmodule

// A communication channel between the shooters.
// Lists all the possible combination of actions, and updates the health of the shooters
// All possible combination of actions must be listed, because a module REEEs if there are two actions active at the same that it can synchronise on
module channel
	[p1wait,p2wait] true -> (p2_lost_hp' = false)&(p1_lost_hp' = false);
	[p1shoot2, p2wait] true -> (health2' = max(health2-1,0)) &(p2_lost_hp' = true)&(p1_lost_hp' = false);
	[p1wait, p2shoot1] true -> (health1' = max(health1-1,0))&(p2_lost_hp' = false)&(p1_lost_hp' = true);
	[p1shoot2, p2shoot1] true -> (health1' = max(health1-1,0)) & (health2' = max(health2-1,0))&(p2_lost_hp' = true)&(p1_lost_hp' = true);
endmodule